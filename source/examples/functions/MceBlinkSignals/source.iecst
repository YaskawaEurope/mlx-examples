// -----------------------------------------------------------------------------
// 40 segments of 50ms
// -----------------------------------------------------------------------------
FB_Segment.PT := T#50ms;
FB_Segment.IN := NOT FB_Segment.Q;
FB_Segment();

IF FB_Segment.Q THEN
  nSegment := nSegment + 1;
END_IF;

IF (nSegment < 1) OR (nSegment > 40) THEN
  nSegment := 1;
END_IF;


// -----------------------------------------------------------------------------
// DoubleFlash: 1...1..... .......... .......... ..........
// -----------------------------------------------------------------------------
CASE nSegment OF
  1, 5:
    io.bDoubleFlash := TRUE;
  ELSE
    io.bDoubleFlash := FALSE;
END_CASE;


// -----------------------------------------------------------------------------
// Slow:  1111111111 .......... 1111111111 ..........
// -----------------------------------------------------------------------------
CASE nSegment OF
  1..10, 21..30:
    io.bSlow := TRUE;
  ELSE
    io.bSlow := FALSE;
END_CASE;


// -----------------------------------------------------------------------------
// Fast:  11111..... 11111..... 11111..... 11111.....
// -----------------------------------------------------------------------------
CASE nSegment OF
  1..5, 11..15, 21..25, 31..35:
    io.bFast := TRUE;
  ELSE
    io.bFast := FALSE;
END_CASE;