nPosTableSize := UINT_TO_INT(SIZEOF(posTable) / SIZEOF(posTable.stEntry[0]));

// -----------------------------------------------------------------------------
// calculate remaining entries until standstill
// -----------------------------------------------------------------------------
nEntriesRemaining := McePosTableRemainingEntries( index := index,
                                                  loadIndex := loadIndex,
                                                  posTableSize := nPosTableSize,
                                                  rollOver := NOT stopAfterLastEntry,
                                                  conditions := conditions,
                                                  posTable := posTable);


// -----------------------------------------------------------------------------
// bitstatus last entry / stop entry
// -----------------------------------------------------------------------------
lastEntry := FALSE;
stopEntry := FALSE;
waitBeforeNextEntry := FALSE;
IF (nEntriesRemaining <= 0) THEN
  MEMUtils.MemSet(pbyBuffer := ADR(conditions), byValue := 0, dwSize := SIZEOF(conditions));
  MEMUtils.MemSet(pbyBuffer := ADR(stConditionResult), byValue := 0, dwSize := SIZEOF(stConditionResult));

  nNextIndex := McePosTableFindEntry(index, 1, nPosTableSize, NOT stopAfterLastEntry, posTable);
  IF nNextIndex >= 0 THEN
    stConditionResult := McePosTableCheckConditions(
      index := nNextIndex,
      posTableSize := nPosTableSize,
      conditions := conditions,
      posTable := posTable);
    waitBeforeNextEntry := NOT stConditionResult.bConditionsOk;
  ELSE
    waitBeforeNextEntry := FALSE;
  END_IF
  
  IF (posTable.stEntry[index].nActionID > 0) THEN
    stopEntry := TRUE;
    // special case: the stopEntry is also the lastEntry
    lastEntry := (McePosTableFindEntry(index, 1, nPosTableSize, FALSE, posTable) = - 1 );
  ELSIF NOT waitBeforeNextEntry THEN
    lastEntry := TRUE;
  END_IF;
END_IF;


// -----------------------------------------------------------------------------
// calculate Queueing Amount (allowed number of motions to be queued)
// -----------------------------------------------------------------------------
// scenario 1; reduce QA till zero now
IF NOT allowQueuing THEN
  QA := QA - 1;
ELSE
  // scenario 2; reduce QA till zero by synchronizing to nEntriesRemaining
  IF (nEntriesRemaining < QA_MAX) THEN
    QA := nEntriesRemaining;
  // scenario 3; use max QA
  ELSE
    QA := QA_MAX;
  END_IF;
END_IF;

IF firstMove OR (QA < 0) THEN
  QA := 0;
END_IF;
