// -----------------------------------------------------------------------------
// common
// -----------------------------------------------------------------------------
// check for new position data
bNewPosData :=
  (MLX.HMIFeedbackData.CurrentRobotTCPCoordFrame = nCoordFrame);

  // -------------------------------------
// lock specific target: frame
  // -------------------------------------
bLockWorld :=
  (io.nLockTarget = 10) OR
  (io.nLockTarget = 20) OR
  (io.nLockTarget = 30) OR
  (io.nLockTarget = 40);

bLockUserFrame :=
  (io.nLockTarget = 12) OR
  (io.nLockTarget = 22) OR
  (io.nLockTarget = 32) OR
  (io.nLockTarget = 42);


// -----------------------------------------------------------------------------
// State machine 1: read actual TCP positions
// -----------------------------------------------------------------------------
CASE io.nSmActualPosition OF
  // -------------------------------------
  // idle
  // -------------------------------------
  0:
    IF MLX.Signals.MLXGatewayConnected THEN
      io.nSmActualPosition := 10;
    ELSE
      nRobotNumber := 0;
      // clear position data
      MEMUtils.MemSet(
        pbyBuffer := ADR(io.aPositions),
        byValue := 0,
        dwSize := SIZEOF(io.aPositions));

      // clear speed data
      MEMUtils.MemSet(
        pbyBuffer := ADR(io.aTcpSpeeds),
        byValue := 0,
        dwSize := SIZEOF(io.aTcpSpeeds));
    END_IF;

  // -------------------------------------
  // read TCP position in world
  // -------------------------------------
  10:
    nCoordFrame := 0;

    IF bNewPosData THEN
      // copy World positions
      MEMUtils.MemCpy(
        pbySrc := ADR(MLX.HMIFeedbackData.CurrentRobotTCPPosition),
        pbyDest := ADR(io.aPositions[nRobotNumber].aWorld),
        dwSize := SIZEOF(io.aPositions[nRobotNumber].aWorld));

      // copy TCP speed (requires S2C1702=1)
      io.aTcpSpeeds[nRobotNumber] :=
        MLX.HMIFeedbackData.CurrentRobotTCPPosition[7];

      IF NOT bLockWorld THEN
        // prepare next
        nCoordFrame := 2;
        io.nSmActualPosition := 20;
      END_IF;
    END_IF;

    IF NOT MLX.Signals.MLXGatewayConnected THEN
      io.nSmActualPosition := 0;
    END_IF;

  // -------------------------------------
  // read TCP position in active user frame
  // -------------------------------------
  20:
    nCoordFrame := 2;

    IF bNewPosData THEN
      // copy user frame positions
      MEMUtils.MemCpy(
        pbySrc := ADR(MLX.HMIFeedbackData.CurrentRobotTCPPosition),
        pbyDest := ADR(io.aPositions[nRobotNumber].aUser),
        dwSize := SIZEOF(io.aPositions[nRobotNumber].aUser));

      // copy TCP speed (requires S2C1702=1)
      io.aTcpSpeeds[nRobotNumber] :=
        MLX.HMIFeedbackData.CurrentRobotTCPPosition[7];

      IF NOT bLockUserFrame THEN
        // prepare next
        nCoordFrame := 0;
        nRobotNumber := nRobotNumber + 1;
        io.nSmActualPosition := 10;
      END_IF;
    END_IF;

    IF NOT MLX.Signals.MLXGatewayConnected THEN
      io.nSmActualPosition := 0;
    END_IF;

  ELSE
    io.nSmActualPosition := 0;
END_CASE;


// -----------------------------------------------------------------------------
// outputs
// -----------------------------------------------------------------------------
io.bPosUpdating := (io.nSmActualPosition <> 0);

// -------------------------------------
// lock specific target: robot
// -------------------------------------
CASE io.nLockTarget OF
  10,
  12:
    nRobotNumber := 0;
  20,
  22:
    nRobotNumber := 1;
  30,
  32:
    nRobotNumber := 2;
  40,
  42:
    nRobotNumber := 3;
END_CASE;

// -------------------------------------
// select feedback position
// -------------------------------------
IF io.bUseFeedbackPosition THEN
  nCoordFrame := nCoordFrame + 10;
END_IF;

// -------------------------------------
// set coord frame selection
// -------------------------------------
MLX.HMIFeedbackConfiguration.CurrentRobotJogCoordFrame := nCoordFrame;

// -------------------------------------
// set robot selection
// -------------------------------------
IF (nRobotNumber < 0) OR (nRobotNumber > (MLX.NumberOfRobots - 1)) THEN
  nRobotNumber := 0;
END_IF;
MLX.HMIFeedbackConfiguration.CurrentRobotNumber := nRobotNumber;


// -----------------------------------------------------------------------------
// read all axis positions
// -----------------------------------------------------------------------------
IF io.bPosUpdating THEN
  FOR i := 0 TO (MLX.NumberOfRobots - 1) DO
    FOR k := 0 TO 5 DO
      io.aPositions[i].aAxis[k] :=
        MLX.Robot[i].RobotAxes[k].FeedbackData.CommandedPosition;
    END_FOR;
  END_FOR;
END_IF;