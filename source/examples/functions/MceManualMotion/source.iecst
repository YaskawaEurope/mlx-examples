// -----------------------------------------------------------------------------
// init
// -----------------------------------------------------------------------------
stIO.bError := FALSE;

fbSelectTool.Enable := FALSE;
fbSetUserFrame.Enable := FALSE;
fbJogAxis.Enable := FALSE;
fbJogTcp.Enable := FALSE;
fbMoveAxisRel.Enable := FALSE;
fbMoveLinRel.Enable := FALSE;
fbIdleTime.IN := FALSE;
fbCoastTime.IN := FALSE;

MEMUtils.MemSet(
  pbyBuffer := ADR(fbJogAxis.Directions),
  byValue := 0,
  dwSize := SIZEOF(fbJogAxis.Directions));

MEMUtils.MemSet(
  pbyBuffer := ADR(fbJogTcp.Directions),
  byValue := 0,
  dwSize := SIZEOF(fbJogTcp.Directions));

MEMUtils.MemSet(
  pbyBuffer := ADR(fbMoveAxisRel.DeltaPosition),
  byValue := 0,
  dwSize := SIZEOF(fbMoveAxisRel.DeltaPosition));

MEMUtils.MemSet(
  pbyBuffer := ADR(fbMoveLinRel.DeltaPosition),
  byValue := 0,
  dwSize := SIZEOF(fbMoveLinRel.DeltaPosition));


// -----------------------------------------------------------------------------
// common
// -----------------------------------------------------------------------------

// set jogging mode as default
stMLX.JoggingMode := stIO.bCmdEnable;

// jog speed
stIO.fSpeed := LIMIT(0.1, stIO.fSpeed, 100);

// detect changes
bMlxNumberChanged := (stIO.nMlxNumber <> nMlxNumberSelected);
bRobotNumberChanged := (stIO.nRobotNumber <> nRobotNumberSelected);
bJogTypeChanged := (stIO.nJogType <> nJogTypeSelected);
bCoordFrameChanged := (stIO.nCoordFrame <> nCoordFrameSelected);
bToolChanged := (stIO.nTool <> nToolSelected);
bUserFrameChanged := (stIO.nUserFrame <> nUserFrameSelected);

IF bMlxNumberChanged OR
  bRobotNumberChanged OR
  bToolChanged OR
  bUserFrameChanged THEN

  bTcpSettingsNeedUpdate := TRUE;
END_IF;


// -----------------------------------------------------------------------------
// jog buttons to directions pos: 1, neg: -1
// -----------------------------------------------------------------------------
bJogPressed := FALSE;
bRunJog := FALSE;
bUseRotSpeed := FALSE;
FOR i := 0 TO 5 DO
  aDirections[i] := 0;
  IF stIO.aJogRobotAxisPos[i] OR stIO.aJogRobotAxisNeg[i] THEN
    bJogPressed := TRUE;
  END_IF;

  IF stIO.aJogRobotAxisPos[i] AND NOT stIO.aJogRobotAxisNeg[i] THEN
    aDirections[i] := 1;
    bRunJog := TRUE;
    IF (nJogTypeSelected = JOGTYPE_TCP) AND (i >= 3) THEN // TCP rotation?
      bUseRotSpeed := TRUE;
    END_IF;
  ELSIF NOT stIO.aJogRobotAxisPos[i] AND stIO.aJogRobotAxisNeg[i] THEN
    aDirections[i] := -1;
    bRunJog := TRUE;
    IF (nJogTypeSelected = JOGTYPE_TCP) AND (i >= 3) THEN // TCP rotation?
      bUseRotSpeed := TRUE;
    END_IF;
  END_IF;
END_FOR;

//block other axis when rotating the TCP
IF bUseRotSpeed THEN
  aDirections[0] := 0;
  aDirections[1] := 0;
  aDirections[2] := 0;
END_IF;

bOsrRunJog := bRunJog AND NOT aOneShots[0];
aOneShots[0] := bRunJog;


// -----------------------------------------------------------------------------
// state machine 1: manual motion
// -----------------------------------------------------------------------------
CASE stIO.nSmManualMotion OF
  // -------------------------------------
  // idle, not ready for jog
  // -------------------------------------
  0:
    IF stIO.bSystemReady AND stIO.bCmdEnable THEN
      stIO.nErrorCode := 0;
      bTcpSettingsNeedUpdate := TRUE;
      stIO.nSmManualMotion := 10;
    END_IF;

  // -------------------------------------
  // ready for jog
  // -------------------------------------
  10:
    nMlxNumberSelected := stIO.nMlxNumber;
    nRobotNumberSelected := stIO.nRobotNumber;
    nJogTypeSelected := stIO.nJogType;
    nCoordFrameSelected := stIO.nCoordFrame;

    IF bOsrRunJog THEN
      stIO.nSmManualMotion := 11;
    END_IF;

    IF NOT stIO.bCmdEnable THEN
      stIO.nSmManualMotion := 0;
    END_IF;

  // -------------------------------------
  // jog selection
  // -------------------------------------
  11:
    nMlxNumberSelected := stIO.nMlxNumber;
    nRobotNumberSelected := stIO.nRobotNumber;
    nJogTypeSelected := stIO.nJogType;
    nCoordFrameSelected := stIO.nCoordFrame;

    IF bRunJog THEN
      // jog axis
      IF (nJogTypeSelected = JOGTYPE_AXIS) THEN
        IF (stIO.bInchingMode) THEN
          stIO.nSmManualMotion := 25;
        ELSE
          stIO.nSmManualMotion := 20;
        END_IF;
      END_IF;

      // jog cartesian
      IF (nJogTypeSelected = JOGTYPE_TCP) THEN
        IF bTcpSettingsNeedUpdate THEN
          stIO.nSmManualMotion := 15;
        ELSE
          IF (stIO.bInchingMode) THEN
            stIO.nSmManualMotion := 35;
          ELSE
            stIO.nSmManualMotion := 30;
          END_IF;
        END_IF;
      END_IF;
    ELSE
      stIO.nSmManualMotion := 10;
    END_IF;

  // -------------------------------------
  // update TCP settings
  // -------------------------------------
  15:
    IF bUpdateSettingsDone THEN
      IF (stIO.bInchingMode) THEN
        stIO.nSmManualMotion := 35;
      ELSE
        stIO.nSmManualMotion := 30;
      END_IF;
    END_IF;

    IF bRobotNumberChanged OR bMlxNumberChanged THEN
      stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
      stIO.nSmManualMotion := 99;
    END_IF;

  // -------------------------------------
  // jog axis with MLxRobotJogAxes
  // -------------------------------------
  20:
    FOR i := 0 TO 5 DO
      fbJogAxis.Directions[i] := aDirections[i];
    END_FOR;
    fbJogAxis.Enable := TRUE;

    IF fbJogAxis.Sts_EN AND fbJogAxis.Sts_DN THEN
      IF fbJogAxis.Sts_ER THEN
        stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
        stIO.nSmManualMotion := 99;
      ELSE
        stIO.nSmManualMotion := 21;
      END_IF;
    END_IF;

    IF bRobotNumberChanged OR bMlxNumberChanged THEN
      stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
      stIO.nSmManualMotion := 99;
    END_IF;

  // -------------------------------------
  // jog axis - idle time
  // -------------------------------------
  21:
    fbIdleTime.IN := TRUE;
    fbCoastTime.IN := TRUE;

    IF fbIdletime.Q THEN
      IF NOT bRunJog OR
        NOT stIO.bCmdEnable OR
        bJogTypeChanged THEN
        stIO.nSmManualMotion := 22;
      ELSE
        stIO.nSmManualMotion := 11;
      END_IF;
    END_IF;

  // -------------------------------------
  // jog axis - coasting
  // -------------------------------------
  22:
    fbCoastTime.IN := TRUE;
    IF fbCoastTime.Q THEN
      stIO.nSmManualMotion := 50;
    END_IF;

  // -------------------------------------
  // inch axis with MLxRobotMoveAxisRelative
  // -------------------------------------
  25:
    FOR i := 0 TO 5 DO
      fbMoveAxisRel.DeltaPosition[i] :=
        (INCHING_DIST_AXIS * SINT_TO_REAL(aDirections[i]));
    END_FOR;

    stMLX.JoggingMode := FALSE;
    fbMoveAxisRel.Enable := TRUE;

    IF fbMoveAxisRel.Sts_EN AND fbMoveAxisRel.Sts_DN THEN
      IF fbMoveAxisRel.Sts_ER THEN
        stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
        stIO.nSmManualMotion := 99;
      END_IF;

      IF fbMoveAxisRel.Sts_PC THEN
        stIO.nSmManualMotion := 50;
      END_IF;
    END_IF;

    IF bRobotNumberChanged OR bMlxNumberChanged THEN
      stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
      stIO.nSmManualMotion := 99;
    END_IF;

  // -------------------------------------
  // jog TCP with MLxRobotJogTCP
  // -------------------------------------
  30:
    FOR i := 0 TO 5 DO
      fbJogTcp.Directions[i] := aDirections[i];
    END_FOR;
    fbJogTcp.Enable := TRUE;

    IF fbJogTcp.Sts_EN AND fbJogTcp.Sts_DN THEN
      IF fbJogTcp.Sts_ER THEN
        stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
        stIO.nSmManualMotion := 99;
      ELSE
        stIO.nSmManualMotion := 31;
      END_IF;
    END_IF;

    IF bRobotNumberChanged OR bMlxNumberChanged THEN
      stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
      stIO.nSmManualMotion := 99;
    END_IF;

  // -------------------------------------
  // jog TCP - idle time
  // -------------------------------------
  31:
    fbIdleTime.IN := TRUE;
    fbCoastTime.IN := TRUE;

    IF fbIdletime.Q THEN
      IF NOT bRunJog OR
        NOT stIO.bCmdEnable OR
        bJogTypeChanged OR
        bCoordFrameChanged OR
        bToolChanged THEN
        stIO.nSmManualMotion := 32;
      ELSE
        stIO.nSmManualMotion := 11;
      END_IF;
    END_IF;

  // -------------------------------------
  // jog TCP - coasting
  // -------------------------------------
  32:
    fbCoastTime.IN := TRUE;
    IF fbCoastTime.Q THEN
      stIO.nSmManualMotion := 50;
    END_IF;

  // -------------------------------------
  // inch TCP with MLxRobotMoveLinearRelative
  // -------------------------------------
  35:
    FOR i := 0 TO 5 DO
      fbMoveLinRel.DeltaPosition.TCPPosition[i] :=
        (INCHING_DIST_TCP * SINT_TO_REAL(aDirections[i]));
    END_FOR;

    stMLX.JoggingMode := FALSE;
    fbMoveLinRel.Enable := TRUE;

    IF fbMoveLinRel.Sts_EN AND fbMoveLinRel.Sts_DN THEN
      IF fbMoveLinRel.Sts_ER THEN
        stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
        stIO.nSmManualMotion := 99;
      END_IF;

      IF fbMoveLinRel.Sts_PC THEN
        stIO.nSmManualMotion := 50;
      END_IF;
    END_IF;

    IF bRobotNumberChanged OR bMlxNumberChanged THEN
      stIO.nErrorCode := 1000 + stIO.nSmManualMotion;
      stIO.nSmManualMotion := 99;
    END_IF;

  // -------------------------------------
  // done
  // -------------------------------------
  50:
    IF NOT bJogPressed THEN
      IF stIO.bSystemReady AND stIO.bCmdEnable THEN
        stIO.nSmManualMotion := 10;
      ELSE
        stIO.nSmManualMotion := 0;
      END_IF;
    END_IF;

  // -------------------------------------
  // state machine error
  // -------------------------------------
  99:
    stIO.bError := TRUE;
    IF NOT bRunJog THEN
      stIO.nSmManualMotion := 0;
    END_IF;

  ELSE
    stIO.nSmManualMotion := 0;
END_CASE;


// -----------------------------------------------------------------------------
// reset
// -----------------------------------------------------------------------------
IF NOT stIO.bsystemReady THEN
  stIO.nSmManualMotion := 0;
END_IF;


// -----------------------------------------------------------------------------
// outputs
// -----------------------------------------------------------------------------
stIO.bIdle := (stIO.nSmManualMotion = 0) OR (stIO.nSmManualMotion = 10);
bUpdateSettings := (stIO.nSmManualMotion = 15);
stIO.bDone := (stIO.nSmManualMotion = 50);
stIO.bBusy := (stIO.nSmManualMotion > 10) AND (stIO.nSmManualMotion < 50);
stIO.bCoasting := (stIO.nSmManualMotion = 22) OR (stIO.nSmManualMotion = 32) ;

FOR i := 0 TO 5 DO
  IF stIO.bBusy THEN
    stIO.aJogRobotAxisNegIndicator[i] := stIO.aJogRobotAxisNeg[i];
    stIO.aJogRobotAxisPosIndicator[i] := stIO.aJogRobotAxisPos[i];
  END_IF;

  IF NOT stIO.bBusy AND NOT stIO.bCoasting THEN
    stIO.aJogRobotAxisNegIndicator[i] := FALSE;
    stIO.aJogRobotAxisPosIndicator[i] := FALSE;
  END_IF
END_FOR;


// -----------------------------------------------------------------------------
// state machine 2: settings
// -----------------------------------------------------------------------------
CASE stIO.nSmSettings OF
  // -------------------------------------
  // idle
  // -------------------------------------
  0:
    IF bUpdateSettings OR
       (bTcpSettingsNeedUpdate AND stIO.bSystemReady AND stIO.bCmdEnable AND stIO.bIdle) THEN
      stIO.nErrorCode := 0;
      stIO.nSmSettings := 10;
    END_IF;

  // -------------------------------------
  // select tool
  // -------------------------------------
  10:
    // init
    MEMUtils.MemSet(
    pbyBuffer := ADR(fbSelectTool.ToolData),
    byValue := 0,
    dwSize := SIZEOF(fbSelectTool.ToolData));

    fbSelectTool.RobotNumber := stIO.nRobotNumber;
    fbSelectTool.ToolNumber := stIO.nTool;
    fbSelectTool.Enable := TRUE;
    IF fbSelectTool.Sts_EN AND fbSelectTool.Sts_DN THEN
      IF fbSelectTool.Sts_ER THEN
        stIO.nErrorCode := 2000 + stIO.nSmSettings;
        stIO.nSmSettings := 99;
      ELSE
        nToolSelected := stIO.nTool;
        stIO.nSmSettings := 20;
      END_IF;
    END_IF;

  // -------------------------------------
  // set user frame
  // -------------------------------------
  20:
    // init
    MEMUtils.MemSet(
    pbyBuffer := ADR(fbSetUserFrame.UserFrameData),
    byValue := 0,
    dwSize := SIZEOF(fbSetUserFrame.UserFrameData));

    fbSetUserFrame.UserFrameData.CoordFrame :=
      stUserFrames.aFrames[stIO.nUserFrame].aVector;

    fbSetUserFrame.RobotNumber := stIO.nRobotNumber;
    fbSetUserFrame.UserFrameNumber := stIO.nUserFrame;
    fbSetUserFrame.Enable := TRUE;

    IF fbSetUserFrame.Sts_EN AND fbSetUserFrame.Sts_DN THEN
      IF fbSetUserFrame.Sts_ER THEN
        stIO.nErrorCode := 2000 + stIO.nSmSettings;
        stIO.nSmSettings := 99;
      ELSE
        nUserFrameSelected := stIO.nUserFrame;
        stIO.nSmSettings := 30;
      END_IF;
    END_IF;

  // -------------------------------------
  // zero-distance motion with MLxRobotMoveAxisRelative (updates actual position)
  // -------------------------------------
  30:
    // init
    MEMUtils.MemSet(
    pbyBuffer := ADR(fbMoveAxisRel.DeltaPosition),
    byValue := 0,
    dwSize := SIZEOF(fbMoveAxisRel.DeltaPosition));

    stMLX.JoggingMode := FALSE;
    fbMoveAxisRel.Enable := TRUE;

    IF fbMoveAxisRel.Sts_EN AND fbMoveAxisRel.Sts_DN THEN
      IF fbMoveAxisRel.Sts_ER THEN
        stIO.nErrorCode := 2000 + stIO.nSmSettings;
        stIO.nSmSettings := 99;
      END_IF;

      IF fbMoveAxisRel.Sts_PC THEN
        nUserFrameSelected := stIO.nUserFrame;
        stIO.nSmSettings := 50;
      END_IF;
    END_IF;

  // -------------------------------------
  // done
  // -------------------------------------
  50:
    bTcpSettingsNeedUpdate := FALSE;
    IF NOT bUpdateSettings THEN
      stIO.nSmSettings := 0;
    END_IF;

  // -------------------------------------
  // state machine error
  // -------------------------------------
  99:
    stIO.bError := TRUE;
    stIO.nSmSettings := 0;

  ELSE
    stIO.nSmSettings := 0;
END_CASE;


// -----------------------------------------------------------------------------
// reset
// -----------------------------------------------------------------------------
IF NOT stIO.bsystemReady THEN
  stIO.nSmSettings := 0;
END_IF;


// -----------------------------------------------------------------------------
// outputs
// -----------------------------------------------------------------------------
bUpdateSettingsDone := (stIO.nSmSettings = 50);


// -----------------------------------------------------------------------------
// FB calls
// -----------------------------------------------------------------------------
fbIdleTime(PT:=stIO.tIdleTime);
fbCoastTime(PT:=stIO.tCoastTime);
fbSelectTool( MLX := stMLX );
fbSetUserFrame( MLX := stMLX );

fbJogAxis(
  RobotNumber := nRobotNumberSelected,
  Speed := stIO.fSpeed,
  MLX:= stMLX);

fbJogTcp(
  RobotNumber := nRobotNumberSelected,
  Speed := stIO.fSpeed,
  UseRotationalSpeed := bUseRotSpeed,
  SpeedUnits := 0,
  CoordFrame := nCoordFrameSelected,
  MLX := stMLX);

fbMoveAxisRel(
  RobotNumber := nRobotNumberSelected,
  BlendFactor := INCHING_BF,
  BlendType := 0,
  Speed := INCHING_SPEED,
  Acceleration := INCHING_ACC_DEC,
  Deceleration := INCHING_ACC_DEC,
  MLX := stMLX );

fbMoveLinRel(
  RobotNumber := nRobotNumberSelected,
  BlendFactor := INCHING_BF,
  BlendType := 0,
  Speed := INCHING_SPEED,
  UseRotationalSpeed := bUseRotSpeed,
  SpeedUnits := 0,
  Acceleration := INCHING_ACC_DEC,
  Deceleration := INCHING_ACC_DEC,
  CoordFrame := nCoordFrameSelected,
  MLX := stMLX);
