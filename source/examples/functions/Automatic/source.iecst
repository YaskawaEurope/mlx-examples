// -----------------------------------------------------------------------------
// common
// -----------------------------------------------------------------------------
bOsrStartProduction := io.bStartProduction AND NOT aOneShots[0];
aOneShots[0] := io.bStartProduction;
bOsrStopProduction := io.bStopProduction AND NOT aOneShots[1];
aOneShots[1] := io.bStopProduction;
bOsrInterruptProduction := io.bInterruptProduction AND NOT aOneShots[2];
aOneShots[2] := io.bInterruptProduction;
bOsrAbortProduction := io.bAbortProduction AND NOT aOneShots[3];
aOneShots[3] := io.bAbortProduction;

bHoldRestartRequested := FALSE;

// -----------------------------------------------------------------------------
// State machine: automatic cycle
// -----------------------------------------------------------------------------
CASE io.nSmAutomatic OF
  // -------------------------------------
  // idle
  // -------------------------------------
  0:
    bAborting := FALSE;
    nCycleCounter := 0;

    IF bOsrStartProduction AND io.bAutomaticMode AND io.bProductionDataIdle
    AND NOT io.bAlarmActive THEN
      io.nSmAutomatic := 10;
    END_IF;

  // -------------------------------------
  // select production
  // -------------------------------------
  10:
    IF io.bProductionActive THEN
      io.nSmAutomatic := 20;
    END_IF;

  // -------------------------------------
  // enable servos
  // -------------------------------------
  20:
    bStartServos := TRUE;

    IF io.bServoOn AND io.bReturnToHomeIdle THEN
      io.nSmAutomatic := 30;
    END_IF;

  // -------------------------------------
  // return to home
  // -------------------------------------
  30:
    IF io.bReturnToHomeDone AND io.bPickPlaceIdle THEN
      nLastPosition := 0;
      io.nSmAutomatic := 40;
    END_IF;

  // -------------------------------------
  // go to pick
  // -------------------------------------
  40:
    IF io.bServoOn AND io.bPickPlaceIdle THEN
      io.nPickPlaceStartPosition := nLastPosition;
      io.nPickPlaceTargetPosition := 1;
      io.bRunPickPlace := TRUE;
    END_IF;

    IF io.bPickPlaceDone THEN
      io.bRunPickPlace := FALSE;
      nLastPosition := io.nPickPlaceTargetPosition;
      io.nSmAutomatic := 50;
    END_IF;

  // -------------------------------------
  // go to place
  // -------------------------------------
  50:
    IF io.bServoOn AND io.bPickPlaceIdle THEN
      io.nPickPlaceStartPosition := nLastPosition;
      io.nPickPlaceTargetPosition := 2;
      io.bRunPickPlace := TRUE;
    END_IF;

    IF io.bPickPlaceDone THEN
      io.bRunPickPlace := FALSE;
      nLastPosition := io.nPickPlaceTargetPosition;
      nCycleCounter := nCycleCounter + 1;

      IF nCycleCounter >= stActiveProductionData.sNumberOfCycles THEN
        io.nSmAutomatic := 60;
      ELSE
        io.nSmAutomatic := 40;
      END_IF;
    END_IF;

  // -------------------------------------
  // return to home
  // -------------------------------------
  60:
    IF io.bServoOn AND io.bPickPlaceIdle THEN
      io.nPickPlaceStartPosition := nLastPosition;
      io.nPickPlaceTargetPosition := 0;
      io.bRunPickPlace := TRUE;
    END_IF;

    IF io.bPickPlaceDone THEN
      io.bRunPickPlace := FALSE;
      nLastPosition := io.nPickPlaceTargetPosition;
      io.nSmAutomatic := 70;
    END_IF;

  // -------------------------------------
  // done
  // -------------------------------------
  70:
    // reset of state machine handled below

  ELSE
    io.nSmAutomatic := 0;

END_CASE;

// Reset
IF NOT io.bEnable THEN
  io.nSmAutomatic := 0;
END_IF;

// -----------------------------------------------------------------------------
// outputs
// -----------------------------------------------------------------------------
io.bIdle := (io.nSmAutomatic = 0);

io.bProductionRunning := (io.nSmAutomatic >= 40) AND (io.nSmAutomatic < 70);

io.bLockProductionData := (io.nSmAutomatic >= 10);

io.bStartServos := (io.nSmAutomatic = 20);

io.bRunReturnToHome := (io.nSmAutomatic = 30);

io.bHoldRestart := bHoldRestartRequested;

io.bHoldPickPlace := ((io.nSmAutomatic >= 50) AND (io.nSmAutomatic <= 55));

io.bFlush := (io.nSmAutomatic = 57);
